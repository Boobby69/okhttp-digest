buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'java'

if (new File("./gradle/localrepo.gradle").exists()) {
    apply from: "./gradle/localrepo.gradle"
}

group = "com.burgstaller"
archivesBaseName = 'okhttp-digest'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenLocal()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

ext {
    okhttpVersion = "3.9.1"
}


dependencies {
    testCompile 'junit:junit:4.12'
    compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    testCompile "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testCompile 'org.mockito:mockito-core:2.10.0'
}

def siteUrl = 'https://github.com/rburgst/okhttp-digest'
def gitUrl = 'https://github.com/rburgst/okhttp-digest.git'

if (!hasProperty('bintray_user')) {
    project.ext {
        bintray_user = "undef"
        bintray_api_key = "undef"
    }
}

bintray {
    user = property('bintray_user')
    key = property('bintray_api_key')

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'android'
        name = project.name
        desc = 'digest authenticator for okhttp'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/rburgst/okhttp-digest/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['android', 'okhttp']
        publicDownloadNumbers = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions {
    doLast {
        println project.getConvention()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}
